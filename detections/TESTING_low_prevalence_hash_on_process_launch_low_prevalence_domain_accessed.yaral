rule TESTING_low_prevalence_hash_on_process_launch_low_prevalence_domain_accessed {
  meta:
    author = "Dan Lussier"
    credit = "Chronicle Security"
    description = "Detects when a low prevalence hash is seen during a process launch event, and a low prevalence domain is accessed within 10m."
    severity = "LOW"

  events:
  		// HASH | PROCESS LOGIC
        $e.metadata.event_type = "PROCESS_LAUNCH"
        $e.principal.ip = $ip

        //join event ($d) to entity graph ($e)
        $d.graph.entity.file.sha256 = $e.target.file.sha256
        $d.graph.metadata.entity_type = "FILE"
        $d.graph.entity.file.prevalence.rolling_max <= 2

        // DOMAIN | HTTP ACCESS LOGIC
        $e1.metadata.event_type = "NETWORK_HTTP"
        $e1.principal.ip = $ip
        // filter out URLs with RFC 1918 IP addresses, i.e., internal assets
        not re.regex($e1.target.hostname, `(127(?:\.(25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)){3}$)|(10(?:\.(25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)){3}$)|(192\.168(?:\.(25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)){2}$)|(172\.(?:1[6-9]|2\d|3[0-1])(?:\.(25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)){2})`)
        // only match valid FQDN, filter out background non-routable noise
        re.regex($e1.target.hostname, `(?:[a-z0-9](?:[a-z0-9-]{0,61}[a-z0-9])?\.)+[a-z0-9][a-z0-9-]{0,61}[a-z0-9]`)
        $domainName = $e1.target.hostname

        //join event ($d1) to entity graph ($e1)
        $d1.graph.entity.domain.name = $e1.target.hostname
        $d1.graph.metadata.entity_type = "DOMAIN_NAME"
        // tune prevalence as fits your results
        $d1.graph.entity.domain.prevalence.rolling_max < 2

  match:
        $ip over 10m

  /* Commented out for now
  outcome:
    $risk_score = max(
        // increment risk score based upon rolling_max prevalence
        if ( $d.graph.entity.domain.prevalence.rolling_max >= 10, 10) +
        if ( $d.graph.entity.domain.prevalence.rolling_max >= 2 and $d.graph.entity.domain.prevalence.rolling_max <= 9 , 20) +
        if ( $d.graph.entity.domain.prevalence.rolling_max = 1, 30)
    )*/

  condition:
    $e and $d and $e1 and $d1
}
